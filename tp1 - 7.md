# Multiplicación de números sin signo
## El programa de abajo utiliza una subrutina para multiplicar dos números, pasando los parámetros por valor para NUM1 y NUM2, y por referencia (RES), en ambos casos a través de la pila.

```assembly
        ORG  3000H      ; Subrutina MUL 
MUL:    PUSH BX         ; preservar registros 
        PUSH CX 
        PUSH AX 
        PUSH DX 
        MOV  BX, SP  
        ADD  BX, 12     ; corrección por el IP(2), 
                        ; RES(2) y los 4 registros(8) 
        MOV  CX, [BX]   ; cx = num2 
        ADD  BX, 2      ; bx apunta a num1 
        MOV  AX, [BX]   ; ax = num1 
        SUB  BX, 4      ; bx apunta a la dir de 
                        ; resultado 
        MOV  BX, [BX]   ; guardo 
        MOV  DX, 0 
SUMA:   ADD  DX, AX 
        DEC  CX 
        JNZ  SUMA 
        MOV  [BX], DX   ; guardar resultado 
        POP  DX         ; restaurar registros 
        POP  AX 
        POP  CX 
        POP  BX 
        RET 
        ORG 1000H       ; Memoria de datos 

NUM1  DW  5H 
NUM2  DW  3H 
RES   DW  ? 
 
        ORG 2000H       ; Prog principal 
                        ; parámetros 
        MOV   AX, NUM1 
        PUSH  AX 
        MOV   AX, NUM2 
        PUSH  AX 
        MOV   AX, OFFSET RES     
        PUSH  AX 
        CALL  MUL 
                        ; desapilar parámetros 
        POP   AX 
        POP   AX 
        POP   AX 
        HLT 
        END 
```

### Analizar su contenido y contestar.
## a) ¿Cuál es el modo de direccionamiento de la instrucción MOV AX, [BX]? ¿Qué se copia en el registro AX en este caso?
Modo indirecto por registro, se copia el valor de la posición de memoria BX interpretada como WORD.
## b) ¿Qué función cumple el registro temporal ri que aparece al ejecutarse una instrucción como la anterior?
Se utiliza para almacenar una dirección de manera temporal
## c) ¿Qué se guarda en AX al ejecutarse MOV AX, OFFSET RES?
Se guarda la posición en memoria de la variable RES.
## d) ¿Cómo se pasa la variable RES a la pila, por valor o por referencia? ¿Qué ventaja tiene esto?
Se pasa por referencia, su ventaja es que podemos editar el contenido en su lugar.
## e) ¿Cómo trabajan las instrucciones PUSH y POP?
La instrucción PUSH apila el operando en la pila de memoria y la instrucción POP recupera en el operando el valor de la pila apuntado por SP (último WORD apilado).
