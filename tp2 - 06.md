## 6) Escribir un programa que solicite el ingreso de un número (de un dígito) por teclado y muestre en pantalla dicho número expresado en letras. Luego que solicite el ingreso de otro y así sucesivamente. Se debe finalizar la ejecución al ingresarse en dos vueltas consecutivas el número cero.

```assembly
	ORG 1000h
MSJ	DB "INGRESE UN NUMERO:", 00h	; mensaje inicial
BYE	DB "ADIOS!", 00h		; mensaje de despedida
CR	DB 0Ah				; LF (Line Feed)

NUM	DB ?				; numero ingresado por teclado
QUIT	DB 0				; contador de ceros consecutivos

					; tabla de numeros en letras
N0	DB "CERO", 00h	
N1	DB "UNO", 00h	
N2	DB "DOS", 00h	
N3	DB "TRES", 00h	
N4	DB "CUATRO", 00h	
N5	DB "CINCO", 00h	
N6	DB "SEIS", 00h	
N7	DB "SIETE", 00h	
N8	DB "OCHO", 00h	
N9	DB "NUEVE", 00h	

	ORG 3000h
LETRAS:         PUSH CX			; guardo CX en pila
		
		MOV CL, 30h		; 0 en ascii
		MOV CH, BYTE PTR [BX]	; copio el valor de NUM a CH
		MOV BX, OFFSET N0	; primer numero en letras

L_LOOP:         CMP CL, CH		; si CL es igual a NUM
		JZ L_EQ			; salto a L_EQ

N_SIG:          CMP BYTE PTR [BX], 00h	; si la letra es 00h
		JZ N_DONE		; salto a N_DONE

		INC BX		 	; siguiente letra
		JMP N_SIG		; salto a N_SIG

N_DONE:         INC BX			; siguiente letra
		INC CL		 	; incremento CL (fila)
		JMP L_LOOP		; salto a L_LOOP

L_EQ:           CALL IMP		; llamo a IMP

		POP CX		 	; recupero CX
		RET		 	; fin de subrutina

	ORG 3500h
IMP:	        PUSH AX			; guardo BX en pila
    PUSH BX			; guardo BX en pila
		MOV AL, 0		; pongo AL en 0 (largo de cadena)

I_SIG:          CMP BYTE PTR [BX], 00h	; si la letra es 00h 
		JZ I_DONE		; salto a I_DONE
		
					; sino
		INC BX			; siguiente letra
		INC AL			; incremento AL (largo de cadena)
		JMP I_SIG		; salto a I_SIG

I_DONE:         POP BX			; recupero BX
		INT 7			; imprimo cadena
		
		POP AX			; recupero AX
		RET			; fin de subrutina

	ORG 2000h
INICIO:         MOV BX, OFFSET MSJ
		CALL IMP		; imprimo mesaje inicial

		MOV BX, OFFSET NUM
		INT 6			; leo por teclado, guardo en NUM

		MOV AL, 1
		INT 7			; imprimo NUM

		CMP NUM, 30h		; si NUM es igual a 0
		JZ CERO			; salto a CERO

		MOV QUIT, 0		; sino pongo QUIT en 0
		JMP CONT		; salto a CONT

CERO:	        INC QUIT		; incremento QUIT

	        CMP QUIT, 2		; si QUIT es igual a 2
		JZ FIN			; salto a FIN

CONT:		MOV BX, OFFSET CR
		INT 7			; imprimo salto de linea

		MOV BX, OFFSET NUM	; paso ref a NUM por BX
		CALL LETRAS		; llamo a letras

		MOV BX, OFFSET CR
		INT 7			; imprimo salto de linea

    	        JMP INICIO		; salto a INICIO

FIN:  	        MOV BX, OFFSET CR
    	        INT 7			; imprimo salto de linea
    
    	        MOV BX, OFFSET BYE
    	        CALL IMP		; imprimo mensaje de despedida
    
    	        INT 0			; HLT
		END                     ; fin del programa
```
